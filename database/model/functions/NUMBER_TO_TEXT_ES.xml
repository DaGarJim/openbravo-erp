<?xml version="1.0"?>
<database name="FUNCTION NUMBER_TO_TEXT_ES">
<function name="NUMBER_TO_TEXT_ES" type="VARCHAR">
<parameter name="p_number" type="NUMERIC" />
<parameter name="p_isMoney" type="CHAR" />
<body><![CDATA[
/*************************************************************************
* The contents of this file are subject to the Openbravo  Public  License
* Version  1.0  (the  "License"),  being   the  Mozilla   Public  License
* Version 1.1  with a permitted attribution clause; you may not  use this
* file except in compliance with the License. You  may  obtain  a copy of
* the License at http://www.openbravo.com/legal/license.html
* Software distributed under the License  is  distributed  on  an "AS IS"
* basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
* License for the specific  language  governing  rights  and  limitations
* under the License.
* The Original Code is Openbravo ERP.
* The Initial Developer of the Original Code is Openbravo SL
* All portions are Copyright (C) 2001-2006 Openbravo SL
* All Rights Reserved.
* Contributor(s):  ______________________________________.
************************************************************************/
TYPE ArrayName IS VARRAY(9) OF VARCHAR2(20) ;
  v_units ArrayName;
  v_tens ArrayName;
  v_hundreds ArrayName;
  v_tenys ArrayName;
  v_twentys ArrayName;
  v_sw             NUMBER; -- sw_dieces_veintes
  v_init           NUMBER := 1; --inicio_tecia
  v_number_to_text VARCHAR2(1000) ; --numero_convertido
  v_number_format  VARCHAR2(20) ; --numero_a_cadena
  v_aux            VARCHAR2(1000) ; --tercia_de_numeros
BEGIN
  v_units := ArrayName('UN ', 'DOS ', 'TRES ', 'CUATRO ', 'CINCO ', 'SEIS ', 'SIETE ', 'OCHO ', 'NUEVE ') ;
  v_tens := ArrayName('DIEZ ', 'VEINTE ', 'TREINTA ', 'CUARENTA ', 'CINCUENTA ', 'SESENTA ', 'SETENTA ', 'OCHENTA ', 'NOVENTA ') ;
  v_hundreds := ArrayName('CIEN ', 'DOSCIENTOS ', 'TRESCIENTOS ', 'CUATROCIENTOS ', 'QUINIENTOS ', 'SEISCIENTOS ', 'SETECIENTOS ', 'OCHOCIENTOS ', 'NOVECIENTOS ') ;
  v_tenys := ArrayName('ONCE ', 'DOCE ', 'TRECE ', 'CATORCE ', 'QUINCE ', 'DIECISEIS ', 'DIECISIETE ', 'DIECIOCHO ', 'DIECINUEVE ') ;
  v_twentys := ArrayName('VEINTIUN ', 'VEINTIDOS ', 'VEINTITRES ', 'VEINTICUATRO ', 'VEINTICINCO ', 'VEINTISEIS ', 'VEINTISIETE ', 'VEINTIOCHO ', 'VEINTINUEVE ') ;
  v_number_format := TO_CHAR(p_number, '000000000000.00') ;
  IF(SUBSTR(v_number_format, 1, 1) = '-') THEN
    v_number_to_text := 'MENOS ';
  END IF;
  FOR i IN 1..5
  LOOP
    v_sw := 0;
    v_aux := REPLACE(SUBSTR(v_number_format, v_init+1, 3), '.', '0') ;
    IF((TO_NUMBER(v_aux) > 0) AND(i = 5)) THEN
      v_number_to_text := v_number_to_text || ' CON ';
    END IF;
    IF(TO_NUMBER(SUBSTR(v_aux, 1, 1)) > 0) THEN
      IF(TO_NUMBER(SUBSTR(v_aux, 1, 1)) = 1 AND TO_NUMBER(SUBSTR(v_aux, 2, 2)) > 0) THEN
        v_number_to_text := v_number_to_text || 'CIENTO ';
      ELSE
        v_number_to_text := v_number_to_text || v_hundreds(TO_NUMBER(SUBSTR(v_aux, 1, 1))) ;
      END IF;
    END IF;
    IF(TO_NUMBER(SUBSTR(v_aux, 2, 2)) > 0) THEN
      IF(TO_NUMBER(SUBSTR(v_aux, 2, 1)) = 1 AND TO_NUMBER(SUBSTR(v_aux, 3, 1)) > 0) THEN
        v_number_to_text := v_number_to_text || v_tenys(TO_NUMBER(SUBSTR(v_aux, 3, 1))) ;
      ELSIF(TO_NUMBER(SUBSTR(v_aux, 2, 1)) = 2 AND TO_NUMBER(SUBSTR(v_aux, 3, 1)) > 0) THEN
        v_number_to_text := v_number_to_text || v_twentys(TO_NUMBER(SUBSTR(v_aux, 3, 1))) ;
      ELSE
        IF(TO_NUMBER(SUBSTR(v_aux, 2, 1)) > 0) THEN
          v_number_to_text := v_number_to_text || v_tens(TO_NUMBER(SUBSTR(v_aux, 2, 1))) ;
        END IF;
        IF(TO_NUMBER(SUBSTR(v_aux, 3, 1)) > 0 AND NOT(i IN(1, 3) AND TO_NUMBER(v_aux) = 1)) THEN
          IF(TO_NUMBER(SUBSTR(v_aux, 2, 1)) > 0) THEN
            v_number_to_text := v_number_to_text || 'Y ';
          END IF;
          v_number_to_text := v_number_to_text || v_units(TO_NUMBER(SUBSTR(v_aux, 3, 1))) ;
        END IF;
      END IF;
    END IF;
    IF((i = 1 OR i = 3) AND TO_NUMBER(v_aux) > 0) THEN
      v_number_to_text := v_number_to_text || 'MIL ';
    ELSIF(i = 2 AND TO_NUMBER(v_aux) > 1) THEN
      v_number_to_text := v_number_to_text || 'MILLONES ';
    ELSIF(i = 2 AND TO_NUMBER(v_aux) = 1) THEN
      v_number_to_text := v_number_to_text || 'MILLON ';
    ELSIF(i = 4) THEN
      IF(p_isMoney = 'Y') THEN
        v_number_to_text := v_number_to_text || ' EUROS';
      END IF;
    ELSIF(i = 5) THEN
      IF(v_aux > 0) THEN
        v_number_to_text := v_number_to_text || ' CENTIMOS';
      END IF;
    END IF;
    v_init := v_init + 3;
  END LOOP;
  RETURN v_number_to_text;
END NUMBER_TO_TEXT_ES]]></body>
</function> 
</database>